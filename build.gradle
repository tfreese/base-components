// Execute Tasks in SubModule: gradle MODUL:clean build
plugins {
    id "com.github.ben-manes.versions" version "$version_gradleVersionsPlugin" apply false
    id "de.freese.gradle.plugin.conventions" version "$version_myConventionPlugin" apply false
    id "io.spring.dependency-management" version "$version_springDependencyManagement" apply false
    id "org.openjfx.javafxplugin" version "$version_javafxPlugin" apply false
    id "org.springframework.boot" version "$version_springBoot" apply false
}

subprojects {
    apply plugin: "base"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "de.freese.gradle.plugin.conventions"

    // Task: help/dependencyUpdates
    apply plugin: "com.github.ben-manes.versions"

    dependencyManagement {
        imports {
            mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
//            mavenBom("org.springframework.boot:spring-boot-dependencies:$version_springBoot")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:$version_springCloud")
        }

        dependencies {
            dependency("com.github.librepdf:openpdf:$version_openpdf")
            dependency("commons-net:commons-net:$version_commonsNet")

            dependency("dev.failsafe:failsafe:$version_failsafe")

            dependencySet(group: "org.bouncycastle", version: "$version_bouncycastle") {
                entry "bcpg-jdk18on"
                entry "bcmail-jdk18on"
            }

            dependency("org.apache.poi:poi-ooxml:$version_poi")
            dependency("org.apache.sshd:sshd-core:$version_sshd")

            dependency("org.jfree:jfreechart:$version_jfreechart") {
                // exclude "com.lowagie:itext"
                // exclude "xml-apis:xml-apis"
            }

            dependencySet(group: "org.openjdk.jmh", version: "$version_jmh") {
                entry "jmh-core"
                entry "jmh-generator-annprocess"
            }

            dependency("org.swinglabs.swingx:swingx-core:$version_swingx")
        }
    }

    afterEvaluate { project ->
        if (project.plugins.hasPlugin("java")
                || project.plugins.hasPlugin("java-library")
                || project.plugins.hasPlugin("application")
                || project.plugins.hasPlugin("war")) {
            dependencies {
                testImplementation("org.awaitility:awaitility")
                testImplementation("org.junit.jupiter:junit-jupiter")
                testImplementation("org.mockito:mockito-junit-jupiter")
                testRuntimeOnly("org.junit.platform:junit-platform-launcher")
                testRuntimeOnly("org.slf4j:slf4j-simple")
            }
        }
    }
}

project("base-calendar") {
    apply plugin: "java"
    description = "Common Classes for Calendar."
    dependencies {

    }
}

project("base-net") {
    apply plugin: "java-library"
    description = "Common Protocols and Classes for the Network Layer."
    dependencies {
        api(project(":base-core"))

        implementation("org.apache.sshd:sshd-core")
        implementation("org.eclipse.angus:jakarta.mail")
        implementation("commons-net:commons-net")
//        implementation("org.slf4j:slf4j-api")
    }
}

project("base-persistence") {
    apply plugin: "java-library"
    description = "Common Patterns and Classes for the Persistence Layer."
//    configurations.configureEach {
//        exclude group: "ch.qos.logback", module: "logback-classic"
//    }
    configurations.testImplementation {
        exclude group: "ch.qos.logback", module: "logback-classic"
    }

    dependencies {
        api(project(":base-core"))

        api("org.apache.tomcat:tomcat-jdbc")

        implementation("org.apache.commons:commons-dbcp2")
        implementation("org.apache.logging.log4j:log4j-to-slf4j")
        implementation("org.springframework.boot:spring-boot-starter-jdbc")
        implementation("org.springframework.boot:spring-boot-starter-data-jpa")

        compileOnly("jakarta.servlet:jakarta.servlet-api")

        testAnnotationProcessor("org.openjdk.jmh:jmh-generator-annprocess")

        testImplementation("com.h2database:h2")
        testImplementation("org.assertj:assertj-core")
        testImplementation("org.apache.derby:derby")
        testImplementation("org.hsqldb:hsqldb")
        testImplementation("org.openjdk.jmh:jmh-core")
        testImplementation("org.xerial:sqlite-jdbc")
    }

//    test {
//        filter {
//            // JMH-Test deaktivieren, weil das mit den Annotation-Processor nicht klappt.
//            excludeTestsMatching "de.freese.base.persistence.jdbc.benchmark.*"
//        }
//    }
}

project("base-reports") {
    apply plugin: "java-library"
    description = "Common Classes for creating Reports or import Office Documents."
    dependencies {
        api(project(":base-core"))

        implementation("com.github.librepdf:openpdf")
        implementation("org.jfree:jfreechart")
        implementation("org.apache.poi:poi-ooxml")
        implementation("org.springframework:spring-core")
    }
}

project("base-resourcemap") {
    apply plugin: "java-library"
    description = "ResourceMaps to use hierarchical ResourceBundles."
    dependencies {
        api(project(":base-core"))

        implementation("org.slf4j:slf4j-api")

        testImplementation("org.slf4j:jcl-over-slf4j")
        testImplementation("org.springframework:spring-core")
    }
}

project("base-security") {
    apply plugin: "java-library"
    description = "Common Classes for the Security Layer."
    dependencies {
        api(project(":base-core"))

        implementation("org.bouncycastle:bcpg-jdk18on")
        implementation("org.bouncycastle:bcmail-jdk18on")
        implementation("org.slf4j:slf4j-api")
    }
}

project("base-swing") {
    apply plugin: "java-library"
    apply plugin: "org.openjfx.javafxplugin"
    description = "Components and Classes for Swing."
    dependencies {
        api(project(":base-net"))
        api(project(":base-resourcemap"))

        javafx {
            version = "$version_javafx"
            modules = ["javafx.base"]
        }

        implementation("com.fasterxml.jackson.core:jackson-databind")
        implementation("org.eclipse.angus:jakarta.mail")
        implementation("jakarta.xml.bind:jakarta.xml.bind-api")
        implementation("org.apache.commons:commons-collections4")
        implementation("org.swinglabs.swingx:swingx-core")
    }
}

project("base-swing-demo") {
    apply plugin: "java"
    description = "Demo for the Swing Components."
    dependencies {
        implementation(project(":base-swing"))

        runtimeOnly("org.glassfish.jaxb:jaxb-runtime")
        runtimeOnly("org.slf4j:slf4j-simple")
    }
}
